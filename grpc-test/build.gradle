plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf'
    id 'com.google.osdetector'
    id "com.github.johnrengelman.shadow"
    id "com.palantir.docker"
}

mainClassName = "org.jzb.test.grpc.GreeterServer"

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    compile "com.github.ixtf.persistence:mongo",
            'io.grpc:grpc-netty-shaded',
            'io.grpc:grpc-protobuf',
            'io.grpc:grpc-stub',
            "io.envoyproxy.protoc-gen-validate:pgv-java-grpc:0.2.0",
            "io.envoyproxy.protoc-gen-validate:pgv-java-stub:0.2.0"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java'
        }
        javapgv {
            artifact = "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:0.2.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            javapgv {
                option "lang=java"
            }
        }
    }
}

docker {
    dependsOn shadowJar
    name "orgjzbtest/${project.name}"
    buildArgs([BUILD_VERSION: "${project.version}"])
    files tasks.shadowJar.outputs
    noCache true
}

shadowJar {
    archiveName "${project.name}-fat.jar"
}